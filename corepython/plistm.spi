open corebase

// Python's list module.

// Creates a python list.
inl create' forall t. : plist t = $"[]"

// Concatenates an array of strings.
inl join' (sep : string) (l : plist string) : string = $"!sep.join(!l)"
// Removes the element at the given index and returns it.
inl pop forall el. (l : plist el) (i : int) : el = $"!l.pop(!i)"
// Appends to an array.
inl push forall el. (l : plist el) (x : el) : () = $"!l.append(!x)"
// Cleards the array.
inl clear forall el. (l : plist el) : () = $"!l.clear()"

// Does on in-place shuffle of the list.
inl shuffle forall t. (deck : plist t) : () =
    global "import random"
    $"random.shuffle(!deck)"

open tensorm

// Converts the tensor into a Numpy one, which use the main system memory. By default, the tensor are using CuPy arrays whose memory resides on the GPU.
inl tensor_to_numpy forall dim el. (x : tensor dim el) : tensor dim el = 
    inl _ : tensor _ _ = flatten x // Asserts the tensor is contiguous.
    inl (tensor {bodies dim}) = x |> apply_ptr
    tensor { dim bodies = real
        struct.map (fun (tensor_body {array stride offset}) =>
            typecase `array with ~ar ~el => 
            tensor_body `dim `el {
                stride offset array = $"!array.get()" : ar el // `get` converts the array to a Numpy one, transferring it to host.
            }) bodies
    }

inl from_2d_tensor forall a b t. (x : tensor (a * b) t) : plist (plist t) =
    inl l, x = create', tensor_to_numpy x
    loop.linear (fst x.dim) fun a =>
        inl l' = create'
        loop.linear (snd x.dim) fun b =>
            push l' (tensor_index (a,b) x)
        push l l'
    l

inl singleton forall t. (x : t) : plist t = $"[!x]"