open corebase
open corecuda
open tensorm
open cardm
open game

type history = sa_listm.sa_list 10 (choice2 action card) // The game history. RL agents need this one.

inl leduc_pickler() : pickler.pu history =
    open pickler
    inl card : pu card = alt {
        King = unit()
        Queen = unit()
        Jack = unit()
    }
    inl action : pu action = alt {
        Fold = unit()
        Call = unit()
        Raise = unit()
    }
    inl action_or_card : pu (choice2 action card) = action ++ card
    inl history : pu history = sa_list action_or_card
    history

inl leduc_conv (table, messages : table * messages) : history = 
    messages |> sa_listm.choose function
        | PlayerGotCard(id,card) when id = table.player_turn => Some(C2of2 card)
        | CommunityCardIs card => Some(C2of2 card)
        | PlayerAction(id,action) => Some(C1of2 action)
        | _ => None

inl leduc_sizes() : ml.cfr.sizes =
    {
        ensemble_id = 4
        world_id = 1 <<< 12
        player_id = 2
        action_id = 1 <<< 2
        seq_id = 1 <<< 4
        thread_id = threads_per_block()
    }

inl leduc_graph() : ml.layers.graph ml.cfr_models.graph_body =
    open ml.layers
    inl pu_history = leduc_pickler()

    inl cfr_sizes = leduc_sizes()
    assert (3 <= cfr_sizes.action_id) "The sizes of action in the table must be >= the length of possible actions."
    inl sizes = {
        ensemble = cfr_sizes.ensemble_id // The third dimension of each weight layer.
        block = blocks_per_grid()
        thread = threads_per_block()
        inner = modup pu_history.size 128
    }

    inl neural =
        (input .input (sizes.block,sizes.thread,sizes.inner) : graph (tensor (int * int * int) float))
        |> matmul_ensemble (sizes.ensemble,sizes.inner,sizes.inner)
        |> binarize' (conv cfr_sizes.world_id) (
            input .output_world_id (sizes.ensemble, sizes.block, sizes.thread) |> apply .ensemble
        )

    inl cfr_table = ml.cfr.graph(cfr_sizes) |> key_graph .cfr_table

    pair neural cfr_table

union alt_action = AA_Raise | AA_Call | AA_Fold

// Converts the alternative actions to the regular ones.
inl action_conv (table : table) (x : alt_action) : action = 
    match x with
    | AA_Fold =>
        inl pot_size_needed_to_call = arraym.reduce max table.pot
        if index table.pot table.player_turn = pot_size_needed_to_call then Call else Fold
    | AA_Call => Call
    | AA_Raise ratio => if table.raises_left > 0 then Raise else Call

inl leduc_output : int -> _ = function
    | 0 => AA_Fold
    | 1 => AA_Call
    | 2 => AA_Raise
    | _ => failwith "Invalid output id in the Leduc model."

open ml.cfr_models
inl game_graph() : cfr_game_graph _ _ =
    cfr_game_graph {
        graph = leduc_graph()
        sizes = leduc_sizes()
        action_mask = fun _ _ _ action_id => action_id < 3
        input = exists leduc_conv, leduc_pickler()
        output = leduc_output
    }

