open game
open corebase
open corecuda
open coreext

union player_type = Computer | Random | Human
type player_types = sam.sa 2 player_type

union event =
    | StartGame
    | PlayerChanged : sam.sa 2 player_type
    | ActionSelected : action

union ui_game_state = 
    | GameNotStarted
    | WaitingForActionFromPlayerId : table
    | GameOver : table

// Whether the state is public or private depends whether it is being sent over the wire.
type state_public =
    {
        ui_game_state : ui_game_state
        pl_type : player_types
        messages : messages
    }

type state_private =
    {
        game : option game_node
        deck : deck
    }

type state_neural =
    {
        model_data : ml.layers.model_data
    }

type state_game = 
    {
        private : state_private
        public : state_public
    }

type state =
    {
        game : state_game
        neural : state_neural
    }

inl init_game() =
    {
        public = {
            ui_game_state = GameNotStarted
            pl_type = arraym.fromList [Computer; Human]
            messages = sa_create
        }
        private = {
            deck = deckm.create()
            game = None
        }
    }

inl init() : state = 
    {
        game = init_game()
        neural = {
            model_data = model.game_graph() |> ml.cfr_models.init |> ml.cfr_models.to_model_data
        }
    }

type state_internal =
    {
        ui_game_state : refm.ref ui_game_state
        pl_type : refm.ref player_types
        game : refm.ref (option game_node)
        deck : refm.ref deck
        messages : refm.ref messages
        rng : refm.ref random.philox_state
    }

// Converts the given state to internal one.
// The refs will point to the original locations.
inl state_to_internal rng (state : state_game) : state_internal =
    {
        ui_game_state = refm.from_local state.public.ui_game_state
        pl_type = refm.from_local state.public.pl_type
        game = refm.from_local state.private.game
        deck = refm.from_local state.private.deck
        messages = refm.from_local state.public.messages
        rng
    }

// Sets the internal state to the values held by the regular one.
inl internal_set (state_internal : state_internal) (state : state_game) =
    open refm
    state_internal.ui_game_state <-# state.public.ui_game_state
    state_internal.pl_type <-# state.public.pl_type
    state_internal.game <-# state.private.game
    state_internal.deck <-# state.private.deck
    state_internal.messages <-# state.public.messages

let play_loop (neural : state_neural) (state : state_internal) node =
    inl push_message = sa_listm.push (refm.to_local state.messages)
    inl pop_deck () = 
        open refm
        inl c,d = deckm.draw_card state.rng #state.deck
        state.deck <-# d
        c

    inl body node =
        open refm
        match node with
        | TerminalFold table =>
            inl msg = 
                inl chips_won = index table.pot table.player_turn
                Showdown{cards_shown=table.pl_card; chips_won winner_id=toggle table.player_turn}
            push_message msg
            state.ui_game_state <-# GameOver(table)
            state.game <-# None
            None
        | TerminalCall table =>
            inl msg =
                inl chips_won, winner_id =
                    inl chips_won = index table.pot table.player_turn
                    match compare_hands table with
                    | Gt => chips_won, 0
                    | Eq => 0, -1
                    | Lt => chips_won, 1
                Showdown{cards_shown=table.pl_card; chips_won winner_id}
            push_message msg
            state.ui_game_state <-# GameOver(table)
            state.game <-# None
            None
        | Round table => 
            match index #state.pl_type table.player_turn with
            | Computer =>
                open model
                inl model = ml.cfr_models.from_model_data model.game_graph() neural.model_data
                inl data = table, refm.to_local state.messages
                console.write_ln "Running the GPU model."
                inl action = ml.cfr_models.run state.rng model (Some data) |> action_conv table
                console.write "The action is: "
                console.write_ln action
                inl msg = PlayerAction(table.player_turn, action)
                push_message msg
                Some (game_round table action)
            | Human => 
                state.ui_game_state <-# WaitingForActionFromPlayerId(table)
                state.game <-# Some node
                None
            | Random =>
                inl action = random_action state.rng table
                inl msg = PlayerAction(table.player_turn, action)
                push_message msg
                Some (game_round table action)
        | RoundWithAction(table,action) => 
            inl msg = PlayerAction(table.player_turn, action)
            push_message msg
            Some (game_round table action)
        | ChanceCommunityCard table =>
            inl card = pop_deck()
            inl msg = CommunityCardIs card
            push_message msg
            Some (game_chance_community_card table card)
        | ChanceInit () => 
            inl c0,c1 = pop_deck(), pop_deck()
            push_message PlayerGotCard(0, c0)
            push_message PlayerGotCard(1, c1)
            Some (game_chance_init (c0,c1))
    
    loop.while (function Some => true | None => false) (optionm.bind body) (Some node) |> ignore

inl event_loop state_neural (msg, state_internal : event * state_internal) : () =
    open refm
    match msg with
    | StartGame =>
        internal_set state_internal init_game()
        play_loop state_neural state_internal game_init()
    | PlayerChanged pl_type => 
        state_internal.pl_type <-# pl_type
    | ActionSelected action =>
        match #state_internal.game with
        | Some game =>
            match game with
            | Round table => play_loop state_neural state_internal RoundWithAction(table, action)
            | _ => failwith "Unexpected game node in ActionSelected."
        | None => failwith "The game hasn't been started in ActionSelected."

open corecuda
open corepython
open coreext
inl main() =
    named_tuple "Leduc_Game" {
        init = fun () => jsonm.serialize init()
        event_loop_gpu = fun (msg, state : _ event * _ state) =>
            open serializer
            inl seri = {
                msg = create_serializer
                game_state = create_serializer
            }
            serialize seri.msg (jsonm.deserialize msg)
            inl {game neural} = jsonm.deserialize state
            serialize seri.game_state game

            console.write_ln "Going to run the Leduc game kernel."
            global "import time"
            inl p : f64 = $"time.perf_counter()"

            run' fun () =>
                inl rng = random.init {seed = random.clock64(); subsequence=conv rangem.threads_in_grid().from; offset=0}
                inl c : _ int = refm.create_shared_var (const threads_per_block())
                inl from = rangem.threads_in_grid().from
                if from = 0 then
                    inl msg, game_state = deserialize seri.msg, deserialize seri.game_state
                    // When converting the state to internal form, the refs will point to the original.
                    event_loop neural (msg, state_to_internal rng game_state)
                    serialize seri.game_state game_state

                // nn model loop
                intrinsic.atomic_add_ref c -1 |> ignore
                loop.while' (const true) fun () =>
                    inl model = ml.cfr_models.from_model_data model.game_graph() neural.model_data
                    ml.cfr_models.run rng model None |> ignore
                    if refm.deref c = 0 then loop.break()

            inl p2 : f64 = $"time.perf_counter()"
            console.write "The time it took to run the kernel (in seconds) is: "
            console.write_ln (p2 - p)

            jsonm.serialize {
                game = deserialize seri.game_state
                neural
                }
    }