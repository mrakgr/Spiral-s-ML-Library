open corebase
open corecuda
open tensorm
open ml.layers
open game

inl nlholdem_input() : pickler.pu history =
    open pickler
    inl card : pu card = alt {
        King = unit()
        Queen = unit()
        Jack = unit()
    }
    inl action : pu action = alt {
        Fold = unit()
        Call = unit()
        Raise = unit()
    }
    inl action_or_card : pu (choice2 action card) = action ++ card
    inl history : pu history = sa_list action_or_card
    history

inl nlholdem_graph() =
    inl pu_history = nlholdem_input()
    inl size = {
        ensemble = 4 // The third dimension of each weight layer.
        block = blocks_per_grid()
        thread = threads_per_block()
        inner = modup pu_history.size 128
    }

    inl graph =
        (input .input (size.block,size.thread,size.inner) : graph (tensor (int * int * int) float))
        |> matmul_ensemble (size.ensemble,size.inner,size.inner)
        |> softmax_and_discrete_sample' (
            pair (input .output_probs   (size.ensemble,size.block,size.thread,size.inner) |> apply .ensemble)
                 (input .output_indices (size.ensemble,size.block,size.thread) |> apply .ensemble)
            )

    graph

union action_model =
    | AA_Fold
    | AA_Call
    | AA_Raise : int * int // numerator * denominator

inl possible_actions () =
    [
        AA_Fold
        AA_Call
        AA_Raise(1, 3)
        AA_Raise(1, 2)
        AA_Raise(2, 3)
        AA_Raise(3, 4)
        AA_Raise(1, 1)
        AA_Raise(3, 2)
        AA_Raise(2, 1)
        AA_Raise(3, 1)
        AA_Raise(limit.max, 1)
    ]

inl nlholdem_output (i' : int) : action_model =
    listm.foldBack 
        (fun (x : action_model) (next : int -> action_model) (i : int) =>
            if i = i' then x else next (i + 1)
            )
        possible_actions() 
        (fun i => failwith "Invalid output id in the NL Holdem model.")
        0

// TODO: table -> action_model -> action

open ml.models
inl game_graph() : game_graph _ _ =
    game_graph {
        graph = exists nlholdem_graph()
        input = nlholdem_input()
        output = nlholdem_output
    }

